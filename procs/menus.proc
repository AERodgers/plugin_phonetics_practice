# Anto's Phonetics Practice: menu procedures
# ==========================================
# Written for Praat 6.0.40 or later
#
# script by Antoin Eoin Rodgers
# rodgeran@tcd.ie
# Phonetics and speech Laboratory, Trinity College Dublin
# Feb 29, 2020
#

# Main menu -------------------------------------------------------------------v
procedure mainMenu: .activities
    .quit = 0
    while not .quit
        # get array of valid activities from table generated by setup.praat
        if .activities > 0
            Read from file: "activities.Table"
            for .i to .activities
                .actProc$[.i]  = Get value: .i, "proc"
                .actTitle$[.i] = Get value: .i, "title"
            endfor
            Remove
        endif

        beginPause: "Anto's phonetic practice"
            choose_activity = 1
            comment: "Main menu"
            comment: "What do you want to do?"
                if .activities > 0
                    optionMenu: "Choose activity", choose_activity
                    for .i to .activities
                        option: .actTitle$[.i]
                    endfor
                endif
        if .activities > 0
            edit_choice = endPause: "Exit", "Update Activities", "Run Activity",
                                ... 3, 1
        else
            edit_choice = endPause: "Exit", "Update Activities",
                                ... 2, 1
        endif
        if edit_choice = 1
            .quit = 1
            beginPause: "Bye bye"
            endPause: "Bye bye", 1, 1
       elsif edit_choice = 2
            .quit = 1
            @updateActivities
        else
            .chosenProc$ = .actProc$[choose_activity]
            @'.chosenProc$'
        endif
    endwhile
endproc

procedure listenContinue: .heading$, .sound
    beginPause: .heading$
        comment: "What do you want to do?"
    edit_choice = endPause: "Listen", "Continue", 1, 1
    if edit_choice = 1
        selectObject: .sound
        Play
        pause
    endif
endproc

procedure listenRepeat: .soundObject
    .plxRepeat = 1
    selectObject: .soundObject
    .soundName$ = selected$("Sound")
    while .plxRepeat
        beginPause: "Sound: " + .soundName$
        comment: "Listen to the phrase"
        .plxRepeat = endPause: "Play", "Next", 1, 1
        .plxRepeat = -.plxRepeat + 2
        if .plxRepeat
            selectObject: .soundObject
            Play
        endif
    endwhile
endproc


procedure keepClear: .objects#, .text$
    beginPause: "Keep or Clear the objects window"
        comment: "Do you want to keep anything from """ + .text$ + """"
        comment: "Select ""Keep"" if you want to have a wee at them look later."
    edit_choice = endPause: "Keep", "Remove", 2, 1

    if edit_choice = 2
        selectObject()
        for .i to size(.objects#)
            plusObject: .objects#[.i]
        endfor
        Remove
    endif
endproc
