#metadataValid
#Simple and complex waveforms
#sawtooth

# Example of a complex waveform
# =============================
# Written for Praat 6.0.40 or later
#
# script by Antoin Eoin Rodgers
# rodgeran@tcd.ie
# Phonetics and speech Laboratory, Trinity College Dublin
# Feb 17, 2020
#
# This script was written to exemplify how complex waveforms can be understood
# as being composed of the sum of a series sinusoidal waveforms.

# main procedure
procedure sawtooth
    # CREATE SOUNDS -----------------------------------------------------------v
    # set f0 parameters
    .f0 = 440

    # create sinusoids
    .objects# = zero#(13)
    for .i to 10
        .objects#[.i] = Create Sound as pure tone:
            ... "tone", 1, 0, 1, 44100, .f0 * (11-.i), 0.4 / (11-.i), 0.01, 0.01
        Rename: "sinusoid_at_" + string$(.f0 * (11-.i)) + "_Hz"
    endfor

    # create sound multi-channel
    selectObject()
    for .i to 10
    plusObject: .objects#[.i]
    endfor
    .multiWave = Combine to stereo
    Rename: "sinusoids_multi-channel"

    # create multi-channel sound
    .singleWave = Convert to mono
    Rename: "Sawtooth_waveform"

    # concatenate original waveforms
    selectObject()
    for .i to 10
    plusObject: .objects#[.i]
    endfor
    .concat = Concatenate
    Reverse
    Rename: "Sinusoids_in_sequence"
    # -------------------------------------------------------------------------^

    # DRAW AND / OR PLAY SOUNDS -----------------------------------------------v
    # draw and play individual sinusoids
    @drawImage: .concat, 4, 0.2, 0, 0, 0.5,  "sinusoids played in sequence"
    # Save as 600-dpi PNG file: "sinusoids_in_sequence.png"
    @listenContinue: "Sinusoids in sequence", .concat

    # draw individual sinusoids
    @drawMultiWave: .multiWave, .f0, 10
    # Save as 600-dpi PNG file: "waveforms_of_individual_sinusoids.png"
    @listenContinue: "All waveforms together (stereo)", .multiWave

    # draw and play sawtooth waveform
    @drawImage: .singleWave, 4, 0.02,  20/.f0, 25/.f0, 0.1,
            ... "sinusoids combined (sawtooth)"
    # Save as 600-dpi PNG file: "sawtooth_waveform.png"
    @listenContinue: "Sawtooth waveform (mono)", .singleWave

    # draw spectrum of sawtooth
    @drawSpectrum: .singleWave, 4, "Sawtooth waveform"
    #Save as 600-dpi PNG file: "sawtooth_spectrum.png"
    @listenContinue: "Spectrum of sawtooth waveform", .singleWave


    # option to clear objects window
    .objects#[11] = .singleWave
    .objects#[12] = .multiWave
    .objects#[13] = .concat
    @keepClear: .objects#, "creating a complex waveform"
endproc

# -----------------------------------------------------------------------------^

# procedures ------------------------------------------------------------------v
procedure drawSpectrum: .sound, .sizeMultiple, .title$
    # Set up picture environment
    Erase all
    Select outer viewport: 0, .sizeMultiple*16/9, 0, .sizeMultiple
    Black
    Courier
    12
    Solid line
    Line width: 1
    Draw inner box

    # Create and draw spectrum
    selectObject: .sound
    .spectrum = To Spectrum: "no"
    Draw: 0, 5000, 20, 70, "no"

    # Draw borders and text
    Line width: 2
    Marks left every: 1, 10, "yes", "yes", "no"
    Marks bottom every: 1000, 1, "yes", "yes", "no"
    Line width: 1
    Marks left every: 1, 2, "no", "yes", "no"
    Marks bottom every: 1000, 0.2, "no", "yes", "no"
    Text left: "yes", "Sound pressure level (dB/Hz)"
    Text bottom: "yes", "Frequency (kHz)"
    14
    Text top: "yes", "##Spectrum of " + .title$
    10

    # Remove spectrum object
    removeObject: .spectrum
endproc

procedure drawMultiWave: .sound, .f0, .harmonics
    # Set up picture environment
    Erase all
    Select outer viewport: 0, 4.5, 0, 8
    Black
    Courier
    12
    Solid line
    Line width: 1
    Draw inner box

    # Draw waveforms
    selectObject: .sound
    Draw: 5/.f0, 7/.f0, 0, 0, "no", "Curve"

    # Draw borders and text
    Axes: 0, 2/.f0, 0, 1
    Marks bottom every: 0.001, 1, "yes", "yes", "no"
    Text bottom: "yes", "Time (ms)"
    Axes: 0, 1, 0, .harmonics
    for .i to .harmonics
        Text: 0, "right", .i - 0.5, "Half", string$(.f0 * .i) + " Hz"
        Draw rectangle: 0, 1, .i, .i - 1
    endfor
    Text top: "yes", "##Waveform of each sinusoid shown individually"
endproc

procedure drawImage: .sound, .sizeMultiple, .timeMark, .start, .end, .absInt,
        ... .title$
    # Set up picture environment
    Erase all
    Select outer viewport: 0, .sizeMultiple*16/9, 0, .sizeMultiple
    Black
    Courier
    12
    Solid line
    Line width: 1

    # Draw waveform
    Select outer viewport: 0, .sizeMultiple*16/9, 0, 1.8
    Draw inner box
    selectObject: .sound
    Draw: .start, .end, -.absInt, .absInt, "no", "Curve"

    # Mark amplitude axis
    Marks left: 3, "yes", "yes", "no"

    # create spectrogram object
    selectObject: .sound
    .spectrogram = To Spectrogram: 0.03, 5000, 0.002, 20, "Gaussian"

    # Draw spectrogram
    Select outer viewport: 0, .sizeMultiple*16/9, 1, .sizeMultiple
    Paint: .start, .end,  0, 0, 100, "yes", 50, 6, 0, "no"

    # Draw borders and text
    Draw inner box
    Line width: 2
    Marks left every: 1000, 0.5, "yes", "yes", "yes"
    Marks bottom every: 1, .timeMark * 5, "yes", "yes", "yes"
    Line width: 1
    Marks left every: 1000, 0.1, "no", "yes", "no"
    Marks bottom every: 1, .timeMark, "no", "yes", "no"
    Text left: "yes", "Frequency (kHz)"
    Text bottom: "yes", "Time (secs)"
    Select outer viewport: 0, .sizeMultiple*16/9, 0, .sizeMultiple
    14
    Text top: "yes", "##Waveform & spectrogram of " + .title$
    10

    # Remove spectrogram object
    removeObject: .spectrogram
endproc
# -----------------------------------------------------------------------------^
